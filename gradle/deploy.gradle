apply plugin: 'maven-publish'
apply plugin: 'signing'

def getValueFromPropertiesFile = { propFile, key ->
    if (!propFile.isFile() || !propFile.canRead())
        return null
    def prop = new Properties()
    def reader = propFile.newReader()
    try {
        prop.load(reader)
    } finally {
        reader.close()
    }
    return prop.get(key)
}

def getProperty = { name, defValue ->
    def prop = project.properties[name] ?:
            getValueFromPropertiesFile(project.rootProject.file('local.properties'), name)
    return (null == prop) ? defValue : prop
}

ext['signing.keyId'] = getProperty('signing.keyId', null)
ext['signing.password'] = getProperty('signing.password', null)
ext['signing.secretKeyRingFile'] = getProperty('signing.secretKeyRingFile', null)
ext.ossrhUsername = getProperty('ossrhUsername', null)
ext.ossrhPassword = getProperty('ossrhPassword', null)

android {
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.dittofederal'
                version '0.0.4'

                pom {
                    name = 'Atak Compose'
                    description = 'Atak Compose'
                    url = 'https://dittofederal.com'

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/license/mit/'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/getditto-federal/atak-compose.git'
                        developerConnection = 'scm:git:ssh://github.com/getditto-federal/atak-compose.git'
                        url = 'https://dittofederal.com'
                    }

                    developers {
                        developer {
                            id = 'ditto'
                            name = 'Ditto'
                            email = 'tak@ditto.live'
                        }
                    }
                }

                pom.withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies.'*'.findAll() {
                        it.artifactId.text() == 'takdevlint'
                    }.each() {
                        it.parent().remove(it)
                    }
                }

            }
        }

        repositories {
            maven {
                name = "sonatype"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}